# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
pkgver=2022.07_rc4.20220621
pkgrel=1
pkgdesc='U-Boot for Apple Silicon Macs'
_commit_id=756d0269dd3f57e3dc7caccf57b78403adbc1e68
_srcname=u-boot-${_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${_commit_id}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commit_id}.tar.gz"
)
sha256sums=('b6d0c74066a85586736930056b9bb74d8a543357582026161e4ae66ddf50577a')
b2sums=('d11f754a0e27e0f136791b2792129fd9c70174f6f882ce43cba0f4c0e807df3c129806ccd9345a7bf596c4f9fcba7b2451392777d0bd0d5063e156dce805e4c8')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
