# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
pkgver=2022.07_rc99.20220712
pkgrel=1
pkgdesc='U-Boot for Apple Silicon Macs'
_commit_id=300817d324f73c30c998a10435d5d830b58df894
_srcname=u-boot-${_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${_commit_id}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commit_id}.tar.gz"
)
sha256sums=('58f93c892e2d345dabd23aad99dd9bface76112660364d75591f0a86aea4173c')
b2sums=('bb301eb45014957fd215933d3f01e886d9b200c5c403abc41aea49f4ba0528d7225a5b3f23161fbeb9f0895ebefe245876c4ec014668d72152c99e5251c2d513')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
