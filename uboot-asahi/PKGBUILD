# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
_ver=2023.07.02
_asahirel=4

pkgver=${_ver}.asahi${_asahirel}
pkgrel=1
pkgdesc='U-Boot for Apple Silicon Macs'
_commit_id=asahi-v${_ver}-${_asahirel}
_srcname=u-boot-${_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${_commit_id}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commit_id}.tar.gz"
)
sha256sums=('ecfd98fa66a152d9b525478a90a7f6ad47950188cbdfa5e11b0c83f62770b9da')
b2sums=('693c4181df35fc26fa4b753014a127c93a5662bdaf7be2391ba2541a9ce4abf988b28e10a90cfb3949ebc5c931060d2313bc59b15a6f12b876326e9f76e59cfb')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
