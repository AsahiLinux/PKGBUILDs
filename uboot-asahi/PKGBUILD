# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
pkgver=2022.07_rc99.20220801
pkgrel=1
pkgdesc='U-Boot for Apple Silicon Macs'
_commit_id=763c472239eaf4d429ea16ed4504f62569e64ce2
_srcname=u-boot-${_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${_commit_id}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commit_id}.tar.gz"
)
sha256sums=('638050995696c2cdd7f48f727439eae6cd10eacba4131342cff31e242bcc0f0b')
b2sums=('63eabc08d290556c31292266fd602fdb7830eb7f88b24d3c7274424a7279a3397ca5b84ce170d6b82c2b8eb8240fd4ee1d2474a398ad91ee9e210552eacf9e3d')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
