# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
_ver=2023.04
_asahirel=1

pkgver=${_ver}.asahi${_asahirel}
pkgrel=1
pkgdesc='U-Boot for Apple Silicon Macs'
_commit_id=asahi-v${_ver}-${_asahirel}
_srcname=u-boot-${_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${_commit_id}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commit_id}.tar.gz"
)
sha256sums=('22284ec27f51f53695bde88b0655e46512a72122e83cc049fda4fb0a049d08e2')
b2sums=('46a2a1b7398fcea58a7c60b7aabd7e7ffc07a47430ac2950cecdf7af4f16125ccea898e8f91e0de3218c90bde298a296d5ffabe961cadf3bf2160f6863c24e10')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
