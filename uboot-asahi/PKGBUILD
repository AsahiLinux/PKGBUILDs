# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
_ver=2022.10
_asahirel=1

pkgver=${_ver}.asahi${_asahirel}
pkgrel=1
pkgdesc='U-Boot for Apple Silicon Macs'
_commit_id=asahi-v${_ver}-${_asahirel}
_srcname=u-boot-${_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${_commit_id}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commit_id}.tar.gz"
)
sha256sums=('58e28abe8805e50ce42b6471162ae1c85484941018b69a441b7b869b1004a90b')
b2sums=('7ee4ab30a05d95028ab80a07e7fa72b85e379a984151eb6888a18fd28948d217168b8579161aea2fbf922e64d76051cde7cbf947ee5c1921ce49fe32ef2835f4')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
