# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
_ver=2022.07
_asahirel=1

pkgver=${_ver}_rc100.asahi${_asahirel}
pkgrel=1
pkgdesc='U-Boot for Apple Silicon Macs'
_commit_id=asahi-v${_ver}-${_asahirel}
_srcname=u-boot-${_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${_commit_id}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commit_id}.tar.gz"
)
sha256sums=('91c26d99f0ee4b29d861352414166ec1f210ac66d297b02b59683001b0fdb844')
b2sums=('5afe0b686cf451f687a99a84ca287390ae5d0ca43880dca26620afbe6161dfa3233001a14a322f00b2e22ba0a683f3857b441612ca4636bf8477da77150595ce')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
