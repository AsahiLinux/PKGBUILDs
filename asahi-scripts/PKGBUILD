# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=asahi-scripts
pkgver=20231019
pkgrel=1
pkgdesc='Asahi Linux maintenance scripts'
arch=('any')
url='http://asahilinux.org'
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/AsahiLinux/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
install=asahi-scripts.install
sha256sums=('1268fdd26e9f3f18bd3e95ad1e6ceb299926fccfffaa6398cf07bd62315a7caa')
b2sums=('1aed07a408d9857b8de2ae9d60fe2b04eca25dce68dbd0090d264f7b89c3a2c275fb902ece13ca3e9ccf0f6e04d5f40a79f8ca0c4114117613c72b02b3cfda97')
backup=(etc/m1n1.conf)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR=${pkgdir} all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR=${pkgdir} install-arch

  install -Dm644 "$srcdir/${pkgname}-${pkgver}/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
