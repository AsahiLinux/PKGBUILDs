# Maintainer: Janne Grunau <j@jannau.net

pkgname=tiny-dfr
pkgver=0.1.1
pkgrel=1
pkgdesc='Apple silicon touch bar input / display daemon'
arch=('aarch64')
url=https://github.com/WhatAmISupposedToPutHere/tiny-dfr
license=('MIT' 'APACHE')
makedepends=(cargo)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('7e61f1b6dac0e559481a6d2db4d994490593a3417fc9aae130ce768064bbaaa6780b7322356b489d7d28040e1b18302194a06803e2ccfa8e97aa94a69c2a96cb')

prepare() {
    cd $pkgname-$pkgver
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
    cd $pkgname-$pkgver
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

package() {
    cd $pkgname-$pkgver
    install -Dm755 target/release/$pkgname "$pkgdir"/usr/bin/$pkgname
    install -Dm644 share/$pkgname/*.svg -t "$pkgdir/usr/share/$pkgname"
    install -Dm644 etc/systemd/system/$pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
    install -Dm644 etc/udev/rules.d/99-touchbar-seat.rules "$pkgdir/usr/lib/udev/rules.d/99-touchbar-seat.rules"
    install -Dm644 etc/udev/rules.d/99-touchbar-tiny-dfr.rules "$pkgdir/usr/lib/udev/rules.d/99-touchbar-tiny-dfr.rules"
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -Dm644 LICENSE.material "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.material
}

# vim:set ts=2 sw=2 et:
