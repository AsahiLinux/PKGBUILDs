# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=m1n1
pkgver=1.4.8
pkgrel=1
pkgdesc='Asahi Linux bootloader'
_artwork_commit_id=35626c31f5c5a8a884aaff93f864795026495742
_m1n1_commit_id=v${pkgver}
_artwork_srcname=artwork-${_artwork_commit_id}
_m1n1_srcname=m1n1-${_m1n1_commit_id##v}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT')
makedepends=( imagemagick )
source=(
  "artwork-${_artwork_commit_id}.tar.gz::https://github.com/AsahiLinux/artwork/archive/${_artwork_commit_id}.tar.gz"
  "m1n1-${_m1n1_commit_id}.tar.gz::https://github.com/AsahiLinux/m1n1/archive/${_m1n1_commit_id}.tar.gz"
)
sha256sums=('ffa129b5cae8debd4ffd97f7f1ac7f571c7ba4679793cd138918c2ae76a7103d'
            'a2141a4a412775d9f25b759611c5d010a1ec6929eadf895a094ca2f7ae37ac28')
b2sums=('26e9668f7c7393fec24b53b184ea0eabed4f46479c1aab26728d01c179091683cc4387ff83796bdcbb289376236faf9f651197b9fbba51e52540e58ebdc8bcc5'
        'c11a1537adbdaf78b388c2b37818de50b7d19ce5fac9d18c3fdd3fb09e7ddb592c780da8d5e129527a1d2673a7eee13f3590d0b833eaa064b2ba1c4642f5753c')

prepare() {
  rm -rf "${srcdir}/$_m1n1_srcname"/artwork
  cp -r "${srcdir}/$_artwork_srcname" "${srcdir}/$_m1n1_srcname"/artwork
}

build() {
  cd "${srcdir}/$_m1n1_srcname"
  make ARCH= RELEASE=1
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "${_m1n1_srcname}/build/m1n1.bin"

  install -Dm644 "$srcdir/$_m1n1_srcname/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
