# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - swap zstd for gzip in depends, remove gzip optdepend
#  - patch to revert back to gzip for default compression
# Asahi: Hector Martin <marcan@marcan.st>
#  - patch to look up modules against the *target* kernel version for autodetect

pkgname=mkinitcpio
pkgver=31
pkgrel=4
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url='https://github.com/archlinux/mkinitcpio'
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'binutils' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd')
makedepends=('asciidoc')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('initramfs')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        '0001-use-gzip-for-compression-by-default.patch'
        '0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch'
        '0003-autodetect-Look-up-modules-in-the-target-kernel-vers.patch'
)
install=mkinitcpio.install
sha512sums=('4ef87c2e4f579b292c38f9c487e78b3b99f6db77909cab322e860e5ca70aca3747fcfc272e2e15c9a3605c924ab178057b8b23151f98debc5d96e65f3c0c49d5'
            'SKIP'
            '692222c01112b30e2d0a0d48265d481617349074bee968c6dca4926bc4ef6501847a79bd7de403ee10ee2e77a7dcc0422ed49a607fe3e96a4d523e2e569724ad'
            '8832c8dd6db5e812f5ea58c42a992c30b9f79cefb4fa513f408441ddaa03bd078118cf95faa004c11b1faf812023b48ff7bb0ffe1b28473c3f305ae691ad5f73'
            'ec3ed850fb17d72fa1ff1e48ca707d0cdab416d760a8efe1a4c08dac11e4484da43a4d897cfc597b73d792e2ce256c802d747957ccbb9b1eb4faebd0f0039b02')
b2sums=('0113e288906e3b5fa485c29c00e7df60d85addd96718c45531031a686f18c739fa18303b6cac374d35b85edb7b663e221c8dc9158dff08c75858a4ed4dd154bf'
        'SKIP'
        '3373b576de3e6fc9d728637f33d7c813bdff20925dc266cab570aea9978927f2d039aa3f8a509b48dcb1c37fd64c65983a1de23db288befdf1005c08dab0f664'
        '8c2df3c08379f3133cfa46919438771e32b22928d1028001b87c8a727c2e8d3ef53c06b9c570cc071753ba072be0f1d2ec6637b68f522fdf6b367b77af7b45f7'
        '1ea9f66d31e1732403e3bd5b7f0ae63759df03a94d190335ae1eb3728f8b9ca1b41108dcf13d55529631c894eeb3df1f7f12c4aa6c47e39875b9a801370f8c19')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB')    # Giancarlo Razzolini

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-use-gzip-for-compression-by-default.patch
  patch -p1 -i ../0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
