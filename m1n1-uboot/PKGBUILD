# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=m1n1-uboot
pkgver=20220317
pkgrel=1
pkgdesc='Asahi Linux bootloader (m1n1 + U-Boot)'
_artwork_commit_id=35626c31f5c5a8a884aaff93f864795026495742
_m1n1_commit_id=v1.0
_uboot_commit_id=72b0eca2e9c4ebe12f1a86769cee6f8ba5903527
_artwork_srcname=artwork-${_artwork_commit_id}
_m1n1_srcname=m1n1-${_m1n1_commit_id##v}
_uboot_srcname=u-boot-${_uboot_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "artwork-${_artwork_commit_id}.tar.gz::https://github.com/AsahiLinux/artwork/archive/${_artwork_commit_id}.tar.gz"
  "m1n1-${_m1n1_commit_id}.tar.gz::https://github.com/AsahiLinux/m1n1/archive/${_m1n1_commit_id}.tar.gz"
  "u-boot-${_uboot_commit_id}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_uboot_commit_id}.tar.gz"
)
install=m1n1-uboot.install
sha256sums=('ffa129b5cae8debd4ffd97f7f1ac7f571c7ba4679793cd138918c2ae76a7103d'
            'a405f59d0677c38c590fd71e54784220e73f187e58b63f585bfca08406150f5f'
            'a6c2ea4fbbfea55a05cf7ea840b1107e284547665838c1d7d811b3f22126ca78')
b2sums=('26e9668f7c7393fec24b53b184ea0eabed4f46479c1aab26728d01c179091683cc4387ff83796bdcbb289376236faf9f651197b9fbba51e52540e58ebdc8bcc5'
        'dfb6e057182957b0f93e27433b5e493566d1c77d24f028f55beee5235039d05afa33a44f591d4d385b4dd78648a1b97cd867a8a977a9328c927524e4fc69585f'
        '33aa7c2deab18085f509513a23f71388702421315b08a64485cd8c05409529c0cd93e73c7f9386ca9c6e7a7be8e12771a32d8dfd33efbd86c5782cd9c843009c')

prepare() {
  rm -rf "${srcdir}/$_m1n1_srcname"/artwork
  cp -r "${srcdir}/$_artwork_srcname" "${srcdir}/$_m1n1_srcname"/artwork

  cd "${srcdir}/$_uboot_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_m1n1_srcname"
  make ARCH= RELEASE=1

  cd "${srcdir}/$_uboot_srcname"
  make

  cd "${srcdir}"
  cat "${_m1n1_srcname}/build/m1n1.bin" \
      "${_uboot_srcname}/arch/arm/dts/"t[86]*.dtb \
      <(gzip -c "${_uboot_srcname}/u-boot-nodtb.bin") \
      > "m1n1-uboot.bin"
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 \
    m1n1-uboot.bin "${_m1n1_srcname}/build/m1n1.bin" \
    "${_uboot_srcname}/u-boot-nodtb.bin"

  install -Dt "$tgtdir/dtb" -m644 \
    "${_uboot_srcname}/arch/arm/dts/"t[86]*.dtb

  install -Dm644 "$srcdir/$_m1n1_srcname/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.m1n1"
}
