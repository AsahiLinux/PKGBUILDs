# Maintainer: Hector Martin <marcan@marcan.st>

_name=asahi-installer
pkgname=asahi-fwextract
pkgver=0.6.7
pkgrel=1
pkgdesc='Asahi Linux firmware extractor'
arch=('any')
url='http://asahilinux.org'
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/AsahiLinux/${_name}/archive/refs/tags/v${pkgver}.tar.gz")
install=asahi-fwextract.install
sha256sums=('0b9119e410a8017754fd14d38304f7a59aac82580419dd78864ff48fa3d91cea')
b2sums=('8c6e6471b1ba20de65adc5ee20f0a7785d60918c6208fc4afdac11f2d293a7f876e82bd97d2d6e59a8115c2dfd83ad8bf70ecc14c47ae6c72c632e01ef5d92d5')
depends=(python "asahi-scripts>=20230606-1" lzfse)
makedepends=(python-build python-installer python-wheel python-setuptools)

build() {
    cd "$_name-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "$_name-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
