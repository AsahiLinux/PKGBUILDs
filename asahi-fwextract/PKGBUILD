# Maintainer: Hector Martin <marcan@marcan.st>

_name=asahi-installer
pkgname=asahi-fwextract
pkgver=0.5pre1
pkgrel=2
pkgdesc='Asahi Linux firmware extractor'
arch=('any')
url='http://asahilinux.org'
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/AsahiLinux/${_name}/archive/refs/tags/v${pkgver}.tar.gz")
install=asahi-fwextract.install
sha256sums=('352d12e0e363def9d12f70d1178ee17519fc8089c3d8d044042e4468470864ec')
b2sums=('3672260077f88e66c22a76efd336a9cbd2bc4a8f5632d0af7a69aed257c2fb9e6ee4e17194baef18c7824eaa1cf1243326764463a846bc9dd3a9954524fe0326')
depends=(python asahi-scripts lzfse)
makedepends=(python-build python-installer python-wheel python-setuptools)

build() {
    cd "$_name-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "$_name-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
