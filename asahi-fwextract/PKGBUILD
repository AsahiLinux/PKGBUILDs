# Maintainer: Hector Martin <marcan@marcan.st>

_name=asahi-installer
pkgname=asahi-fwextract
pkgver=0.6.8
pkgrel=1
pkgdesc='Asahi Linux firmware extractor'
arch=('any')
url='http://asahilinux.org'
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/AsahiLinux/${_name}/archive/refs/tags/v${pkgver}.tar.gz")
install=asahi-fwextract.install
sha256sums=('645dc2aa5d7efc138548d9ebf89e37d2b5b965c636b45acf9672ea8a6441bd2e')
b2sums=('72ef737639f917390d937738e6f976f1b6f348a53660a6f20ab17000bbb753e80861a63fd972e72ddc877970685c341f4fed1ef0279ca2fb7cc6108e918ee2b8')
depends=(python "asahi-scripts>=20230606-1" lzfse)
makedepends=(python-build python-installer python-wheel python-setuptools)

build() {
    cd "$_name-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "$_name-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
